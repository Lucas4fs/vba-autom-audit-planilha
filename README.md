<p align="center">
    <h1 align="center">
        洶洫 PREENCHIMENTO/AUDITORIA DE PLANILHA NO EXCEL   AUTOMATIZADO COM VBA 洽洙
    </h1>
    <br>
    <img src="Imagens\CapaProjetoVBA.png">
</p>

<h2>
    淘 SUMﾃヽIO
</h2>

1. [INTRODUﾃﾃグ](#1-introduﾃｧﾃ｣o)<br>
   1.1 - [Intuito do Projeto](#11-intuito-do-projeto)<br>
   1.2 - [Situaﾃｧﾃ｣o Abordada](#12-situaﾃｧﾃ｣o-abordada)<br>
2. [DESENVOLVIMENTO](#2-desenvolvimento)<br>
3. [CONCLUSﾃグ](#3-conclusﾃ｣o)
4. [FERRAMENTAS UTILIZADAS](#4-ferramentas-utilizadas)

## 1 INTRODUﾃﾃグ

### 1.1 Intuito do Projeto

<p>
    O intuito do projeto ﾃｩ mostrar como usar programaﾃｧﾃ｣o para automatizar parte de uma tarefa que ﾃｩ realizada manualmente, alﾃｩm de economizar tempo ﾃｩ possﾃｭvel ter 100% de acertividade no resultado da tarefa dﾃｪs de que todos os requisitos sejam cumpridos corretamente.
</p>

### 1.2 Situaﾃｧﾃ｣o Abordada

<p>
   Um supermercado tem uma planilha que registra a saﾃｭda de todas as notas fiscais(vendas) contendo vﾃ｡rias informaﾃｧﾃｵes, entre elas estﾃ｣o os impostos de ICMS e PIS/COFINS que sﾃ｣o pagos encima daqueles produtos que estﾃ｣o saindo do supermercado, para ter certeza de que o mercado estﾃ｡ pagando a quantidade correta de imposto ﾃｩ feita uma auditoria comparando a planilha "Cliente.xlsm"(contﾃｩm os dados inseridos pelo supermercado) com a planilha "Audit.xlsm" (contﾃｩm a auditoria dos dados inseridos pelo cliente)
</p>

## 2 DESENVOLVIMENTO

<p>
Abaixo serﾃ｣o disponibilizados os arquivos jﾃ｡ prontos para uso, mas tambﾃｩm serﾃ｡ feita a explicaﾃｧﾃ｣o do zero para que todo o processo possa ser entendido:
</p>

- Planilha "Cliente.xlsm"

[DOWNLOAD](Cliente.xlsm)

- Planilha "Audit.xlsm"

[DOWNLOAD](Audit.xlsm)

<p>
  Dentro da planilha "Cliente.xlsm" existem vﾃ｡rias colunas, cada uma tem sua funﾃｧﾃ｣o, abaixo serﾃ｡ detalhado as funﾃｧﾃｵes das colunas que iremos usar:
</p>

 - Consideraﾃｧﾃｵes ICMS

<p>
 Preenchida pelo auditor para informar se o ICMS inserido pelo cliente estﾃ｡ correto ou incorreto, se estiver incorreto ﾃｩ necessﾃ｡rio informar a porcentagem correta da alﾃｭquota.
</p>

- Consideraﾃｧﾃｵes PIS/COFINS

<p>
 Preenchida pelo auditor para informar se o PIS/COFINS inserido pelo cliente estﾃ｡ correto ou incorreto, se estiver incorreto ﾃｩ necessﾃ｡rio informar o regime correto.
</p>

- codigo_produto

<p>
 Informa o cﾃｳdigo de identificaﾃｧﾃ｣o do produto.
</p>

- descricao_produto

<p>
 Informa o nome do produto.
</p>

- CST_ICMS

<p>
 Informa o CST do ICMS usado pelo supermercado.
</p>

- CST_ICMS_Audit

<p>
 Informa o CST do ICMS usado pelo auditor.
</p>

- Aliquota_ICMS

<p>
 Informa a alﾃｭquota nﾃ｣o efetiva do produto inserida pelo supermercado.
</p>

- Aliquota_Efet_ICMS

<p>
 Informa a alﾃｭquota efetiva do produto inserida pelo supermercado (essa ﾃｩ a alﾃｭquota definitiva que ﾃｩ descontada em nota).
</p>

- Alﾃｭquota_ICMS_Audit

<p>
 Informa alﾃｭquota efetiva inserida pelo auditor.
</p>

- CST_PIS

<p>
 Informa o CST do PIS inserido pelo supermercado
</p>

- CST_PIS_Audit

<p>
 Informa o CST do PIS inserido pelo auditor.
</p>

- CST_Cofins

<p>
 Informa o CST do COFINS inserido pelo supermercado
</p>

- CST_Cofins_Audit

<p>
 Informa o CST do COFINS inserido pelo auditor
</p>

- Num Nf

<p>
 Informa o nﾃｺmero da nota fiscal inserido pelo supermercado
</p>

<p>
  Dentro da planilha "Audit.xlsm" na aba "Exceﾃｧﾃｵes de ST Alﾃｭquota e ST" sﾃ｣o cadastrados os mesmos produtos que o cliente possui na planilha "Cliente.xlsm", mas com a alﾃｭquota de ICMS correta
</p>

<img src="Imagens\colunasAudit.png">

<p>
  Dentro da planilha "Audit.xlsm" na aba "Exceﾃｧﾃｵes PIS Cofins Aliq 0" sﾃ｣o cadastrados os mesmos produtos que o cliente possui na planilha "Cliente.xlsm", mas com o PIS e COFINS correto.
</p>

<img src="Imagens\colunasPISeCOFINS.png">

<p>
  Com as planilhas abertas apertamos "ALT + F11" para abrir o ambiente de desenvolvimento VBA, expandimos as pastas do VBAProject (Cliente.xlsm) e inserimos um novo mﾃｳdulo:
</p>

<img src="Imagens\criandoPrimeiraModulo.png">

<p>
  Copie e cole o cﾃｳdigo abaixo no mﾃｳdulo criado:
</p>

```vba
Sub ConferirAliquotas()

' Desabilitar atualizaﾃｧﾃｵes da tela para melhorar a performance
    Application.ScreenUpdating = False
    
    ' Definir variﾃ｡veis para as planilhas e abas
    Dim wsCliente As Worksheet
    Dim wbAudit As Workbook
    Dim wsExcecoesST As Worksheet
    
    ' Definir variﾃ｡veis para as colunas
    Dim colCodigoProduto As Long
    Dim colAliquotaEfetICMS As Long
    Dim colConsideracoes As Long
    Dim colCSTICMS As Long
    Dim colCodigoDeBarras As Long
    Dim colAliquota As Long
    Dim colIsencao As Long
    Dim colST As Long
    Dim colCBENEF As Long
    
    ' Definir planilhas e abas
    Set wsCliente = ThisWorkbook.Worksheets("Planilha1")
    Set wbAudit = Workbooks("Audit.xlsm")
    Set wsExcecoesST = wbAudit.Worksheets("Exceﾃｧﾃｵes de ST Alﾃｭquota e ST")
    
    ' Obter o ﾃｭndice das colunas na planilha "Cliente.xlsm"
    colCodigoProduto = ObterIndiceColunaPorNome(wsCliente, "codigo_produto")
    colAliquotaEfetICMS = ObterIndiceColunaPorNome(wsCliente, "Aliquota_Efet_ICMS")
    colConsideracoes = ObterIndiceColunaPorNome(wsCliente, "Consideraﾃｧﾃｵes ICMS")
    colCSTICMS = ObterIndiceColunaPorNome(wsCliente, "CST_ICMS")
    
    ' Obter o ﾃｭndice das colunas na planilha "Audit.xlsm"
    colCodigoDeBarras = ObterIndiceColunaPorNome(wsExcecoesST, "Cﾃｳdigodebarras")
    colAliquota = ObterIndiceColunaPorNome(wsExcecoesST, "Alﾃｭquota")
    colIsencao = ObterIndiceColunaPorNome(wsExcecoesST, "Isenﾃｧﾃ｣o")
    colST = ObterIndiceColunaPorNome(wsExcecoesST, "ST?")
    colCBENEF = ObterIndiceColunaPorNome(wsExcecoesST, "CBNEF")
    
    ' Verificar se todas as colunas foram encontradas
    If colCodigoProduto = -1 Or colAliquotaEfetICMS = -1 Or colConsideracoes = -1 Or colCodigoDeBarras = -1 Or colAliquota = -1 Or colCSTICMS = -1 Or colIsencao = -1 Or colST = -1 Or colCBENEF = -1 Then
        MsgBox "Uma ou mais colunas nﾃ｣o foram encontradas. Verifique os nomes das colunas.", vbCritical
        Application.ScreenUpdating = True
        Exit Sub
    End If
    
    ' Verificar se hﾃ｡ cﾃｩlulas selecionadas na planilha "Audit.xlsm"
    Dim selRange As Range
    Set selRange = Selection
    If selRange Is Nothing Then
        MsgBox "Por favor, selecione ao menos uma cﾃｩlula na planilha 'Audit.xlsm'.", vbExclamation
        Application.ScreenUpdating = True
        Exit Sub
    End If
    
    ' Percorrer cada linha selecionada na planilha "Audit.xlsm"
    Dim cel As Range
    For Each cel In selRange.Rows
        Dim linhaAudit As Long
        linhaAudit = cel.Row
        Dim codigoDeBarras As String
        codigoDeBarras = CStr(wsExcecoesST.Cells(linhaAudit, colCodigoDeBarras).Value)
        
        ' Percorrer todas as linhas da planilha "Cliente.xlsm"
        Dim lastRowCliente As Long
        lastRowCliente = wsCliente.Cells(wsCliente.Rows.Count, colCodigoProduto).End(xlUp).Row
        
        Dim i As Long
        For i = 2 To lastRowCliente
            Dim codigoProduto As String
            codigoProduto = CStr(wsCliente.Cells(i, colCodigoProduto).Value)
            
            ' Verificar se o cﾃｳdigo de barras da planilha "Audit.xlsm" estﾃ｡ na planilha "Cliente.xlsm"
            If codigoProduto = codigoDeBarras Then
                ' Comparar alﾃｭquotas
                Dim aliquotaEfet As String
                aliquotaEfet = CStr(wsCliente.Cells(i, colAliquotaEfetICMS).Value)
                Dim aliquotaParam As String
                aliquotaParam = CStr(wsExcecoesST.Cells(linhaAudit, colAliquota).Value)
                
                ' Verificar as novas condiﾃｧﾃｵes
                Dim cstICMS As String
                cstICMS = Trim(CStr(wsCliente.Cells(i, colCSTICMS).Value)) ' Ajuste para tratar espaﾃｧos em branco
                
                Dim isencaoParam As String
                isencaoParam = LCase(CStr(wsExcecoesST.Cells(linhaAudit, colIsencao).Value))
                
                Dim stParam As String
                stParam = LCase(CStr(wsExcecoesST.Cells(linhaAudit, colST).Value))
                
                Dim cbenefParam As String
                cbenefParam = Trim(CStr(wsExcecoesST.Cells(linhaAudit, colCBENEF).Value))
                
                ' Condiﾃｧﾃｵes adicionais
                If cbenefParam = "GO822019" Then
                    wsCliente.Cells(i, colConsideracoes).Value = "Produto de padaria, lanchonete ou confeitaria, se enquadra em reduﾃｧﾃ｣o para 7% de acordo com o Parecer."
                ElseIf (cstICMS = "0" Or cstICMS = "20" Or cstICMS = "40" Or cstICMS = "") And cbenefParam = "GO800004" Then
                    wsCliente.Cells(i, colConsideracoes).Value = "Produto nﾃ｣o tributado"
                ElseIf (cstICMS = "0" Or cstICMS = "") And cbenefParam = "GO821022" Then
                    wsCliente.Cells(i, colConsideracoes).Value = "Produto se enquadra em reduﾃｧﾃ｣o de 19% para 12%"
                ElseIf (cstICMS = "0" Or cstICMS = "" Or cstICMS = "20") And stParam = "st" Then
                    wsCliente.Cells(i, colConsideracoes).Value = "Produto se enquadra em ST"
                ElseIf (cstICMS = "0" Or cstICMS = "" Or cstICMS = "20" Or cstICMS = "") And isencaoParam = "isenﾃｧﾃ｣o" Then
                    wsCliente.Cells(i, colConsideracoes).Value = "Produto se enquadra em isenﾃｧﾃ｣o"
                ElseIf cstICMS = "20" And stParam = "nﾃ｣ost" And isencaoParam = "sem isenﾃｧﾃ｣o" And cbenefParam = "" Then
                    wsCliente.Cells(i, colConsideracoes).Value = "Produto nﾃ｣o se enquadra em reduﾃｧﾃ｣o"
                ElseIf cstICMS = "40" And isencaoParam <> "isenﾃｧﾃ｣o" Then
                    wsCliente.Cells(i, colConsideracoes).Value = "Produto nﾃ｣o se enquadra em isenﾃｧﾃ｣o"
                ElseIf cstICMS = "60" And stParam <> "st" Then
                    wsCliente.Cells(i, colConsideracoes).Value = "Produto nﾃ｣o se enquadra em ST"
                ElseIf (cstICMS = "0" Or cstICMS = "") And stParam = "red" And (cbenefParam = "GO821019" Or cbenefParam = "GO821010") Then
                    wsCliente.Cells(i, colConsideracoes).Value = "Produto se enquadra em reduﾃｧﾃ｣o para 7%"
                ElseIf (cstICMS = "0" Or cstICMS = "") And stParam = "red" And cbenefParam = "GO821008" Then
                    wsCliente.Cells(i, colConsideracoes).Value = "Produto se enquadra em reduﾃｧﾃ｣o de 19% para 7%"
                ElseIf (cstICMS = "0" Or cstICMS = "") And stParam = "red" And cbenefParam = "GO821020" Then
                    wsCliente.Cells(i, colConsideracoes).Value = "Produto se enquadra em reduﾃｧﾃ｣o de 19% para 9%"
                ElseIf (cstICMS = "0" Or cstICMS = "") And cbenefParam = "GO821022" Then
                    wsCliente.Cells(i, colConsideracoes).Value = "Produto se enquadra em reduﾃｧﾃ｣o de 19% para 12%"
                ElseIf (cstICMS = "0" Or cstICMS = "") And (aliquotaEfet <> "21") And aliquotaParam = "21" Then
                    wsCliente.Cells(i, colConsideracoes).Value = "Alﾃｭquota Incorreta (ICMS 19% + 2% FCP)"
                ElseIf (cstICMS = "0" Or cstICMS = "") And (aliquotaEfet <> "27") And (aliquotaParam = "27") Then
                    wsCliente.Cells(i, colConsideracoes).Value = "Alﾃｭquota Incorreta (ICMS 25% + 2% FCP)"
                ElseIf cstICMS = "40" And aliquotaEfet <> "0" And isencaoParam = "isenﾃｧﾃ｣o" Then
                    wsCliente.Cells(i, colConsideracoes).Value = "Produto se enquadra em isenﾃｧﾃ｣o"
                ElseIf cstICMS = "60" And aliquotaEfet <> "0" And stParam = "st" Then
                    wsCliente.Cells(i, colConsideracoes).Value = "Produto se enquadra em ST"
                ElseIf aliquotaEfet = aliquotaParam Then
                    wsCliente.Cells(i, colConsideracoes).Value = "Ok Conferido"
                ElseIf cstICMS = "41" And (stParam = "red" Or stParam = "nﾃ｣ost") And isencaoParam <> "isenﾃｧﾃ｣o" Then
                    wsCliente.Cells(i, colConsideracoes).Value = "Produto Tributado"
                Else
                    ' Se nenhuma condiﾃｧﾃ｣o especﾃｭfica for atendida, manter como "Ok Conferido" ou "Alﾃｭquota Incorreta"
                    If aliquotaEfet = "21" And aliquotaParam = "21" Then
                        wsCliente.Cells(i, colConsideracoes).Value = "Ok Conferido"
                    ElseIf aliquotaEfet = "27" And aliquotaParam = "27" Then
                        wsCliente.Cells(i, colConsideracoes).Value = "Ok Conferido"
                    Else
                        wsCliente.Cells(i, colConsideracoes).Value = "Alﾃｭquota Incorreta"
                    End If
                End If
            End If
        Next i
    Next cel
    
    ' Reativar atualizaﾃｧﾃｵes da tela
    Application.ScreenUpdating = True

End Sub

' Funﾃｧﾃ｣o auxiliar para obter o ﾃｭndice da coluna por nome
Function ObterIndiceColunaPorNome(ws As Worksheet, nomeColuna As String) As Long
    Dim cel As Range
    For Each cel In ws.Rows(1).Cells
        If cel.Value = nomeColuna Then
            ObterIndiceColunaPorNome = cel.Column
            Exit Function
        End If
    Next cel
    ObterIndiceColunaPorNome = -1
End Function
```
<p>
  Crie outro mﾃｳdulo
</p>

<img src="Imagens\criandoSegundoModulo.png">

<p>
    Cole o cﾃｳdigo abaixo:
</p>

```vba
Sub ConferirPis()

    ' Desabilitar atualizaﾃｧﾃｵes da tela para melhorar a performance
    Application.ScreenUpdating = False
    
    ' Definir variﾃ｡veis para as planilhas e abas
    Dim wsCliente As Worksheet
    Dim wbAudit As Workbook
    Dim wsExcecoesPIS As Worksheet
    
    ' Definir variﾃ｡veis para as colunas
    Dim colCodigoProduto As Long
    Dim colcstpis As Long ' CST_PIS
    Dim colConsideracoespis As Long ' Consideraﾃｧﾃｵes PIS/COFINS
    
    Dim colCodigoDeBarras As Long ' Codigo de Barras
    Dim cstPisParam As Long
    
    ' Definir planilhas e abas
    Set wsCliente = ThisWorkbook.Worksheets("Planilha1")
    Set wbAudit = Workbooks("Audit.xlsm")
    Set wsExcecoesPIS = wbAudit.Worksheets("Exceﾃｧﾃｵes PIS Cofins Aliq 0")
    
    ' Obter o ﾃｭndice das colunas na planilha "Cliente.xlsm"
    colCodigoProduto = ObterIndiceColunaPorNome(wsCliente, "codigo_produto")
    colcstpis = ObterIndiceColunaPorNome(wsCliente, "CST_PIS")
    colConsideracoespis = ObterIndiceColunaPorNome(wsCliente, "Consideraﾃｧﾃｵes PIS/COFINS")
    
    ' Obter o ﾃｭndice das colunas na planilha "Audit.xlsm"
    colCodigoDeBarras = ObterIndiceColunaPorNome(wsExcecoesPIS, "CodBarras2") ' CodBarras2 ao invﾃｪs de Codigo de Barras
    cstPisParam = ObterIndiceColunaPorNome(wsExcecoesPIS, "CST")
    
    ' Verificar se todas as colunas foram encontradas
    If colCodigoProduto = -1 Or colcstpis = -1 Or colConsideracoespis = -1 Or colCodigoDeBarras = -1 Or cstPisParam = -1 Then
        MsgBox "Uma ou mais colunas nﾃ｣o foram encontradas. Verifique os nomes das colunas.", vbCritical
        Application.ScreenUpdating = True
        Exit Sub
    End If
    
    ' Verificar se hﾃ｡ cﾃｩlulas selecionadas na planilha "Audit.xlsm"
    Dim selRange As Range
    Set selRange = Selection
    If selRange Is Nothing Then
        MsgBox "Por favor, selecione ao menos uma cﾃｩlula na planilha 'Audit.xlsm'.", vbExclamation
        Application.ScreenUpdating = True
        Exit Sub
    End If
    
    ' Percorrer cada linha selecionada na planilha "Audit.xlsm"
    Dim cel As Range
    For Each cel In selRange.Rows
        Dim linhaAudit As Long
        linhaAudit = cel.Row
        Dim codigoDeBarras As String
        codigoDeBarras = CStr(wsExcecoesPIS.Cells(linhaAudit, colCodigoDeBarras).Value)
        
        ' Percorrer todas as linhas da planilha "Cliente.xlsm"
        Dim lastRowCliente As Long
        lastRowCliente = wsCliente.Cells(wsCliente.Rows.Count, colCodigoProduto).End(xlUp).Row
        
        Dim i As Long
        For i = 2 To lastRowCliente
            Dim codigoProduto As String
            codigoProduto = CStr(wsCliente.Cells(i, colCodigoProduto).Value)
            
            ' Verificar se o cﾃｳdigo de barras da planilha "Audit.xlsm" estﾃ｡ na planilha "Cliente.xlsm"
        If codigoProduto = codigoDeBarras Then
                ' Comparar pis
                Dim pis As String
                pis = CStr(wsCliente.Cells(i, colcstpis).Value)
                Dim pisParam As String
                pisParam = CStr(wsExcecoesPIS.Cells(linhaAudit, cstPisParam).Value)
                
                ' Condiﾃｧﾃ｣o: se o pisParam for igual ao pis
            If pisParam = pis Then
                ' Inserir na coluna Consideraﾃｧﾃｵes PIS/COFINS o valor "Ok Conferido"
                wsCliente.Cells(i, colConsideracoespis).Value = "Ok Conferido"
                
                ' Condiﾃｧﾃ｣o: se o pisParam for igual a 1 e o pis for diferente de 1
                ElseIf pisParam = "1" And pis <> "1" Then
                ' Inserir na coluna Consideraﾃｧﾃｵes PIS/COFINS o valor "Produto Tributado"
                wsCliente.Cells(i, colConsideracoespis).Value = "Produto Tributado"
            
                ' Condiﾃｧﾃ｣o: se o pisParam for igual a 4 e o pis for diferente de 4
                ElseIf pisParam = "4" And pis <> "4" Then
                ' Inserir na coluna Consideraﾃｧﾃｵes PIS/COFINS o valor "Produto Monofﾃ｡sico"
                wsCliente.Cells(i, colConsideracoespis).Value = "Produto Monofﾃ｡sico"
                
                ' Condiﾃｧﾃ｣o: se o pisParam for igual a 5 e o pis for diferente de 5
                ElseIf pisParam = "5" And pis <> "5" Then
                ' Inserir na coluna Consideraﾃｧﾃｵes PIS/COFINS o valor "Substituiﾃｧﾃ｣o Tributﾃ｡ria"
                wsCliente.Cells(i, colConsideracoespis).Value = "Substituiﾃｧﾃ｣o Tributﾃ｡ria"
                
                ' Condiﾃｧﾃ｣o: se o pisParam for igual a 6 e o pis for diferente de 6
                ElseIf pisParam = "6" And pis <> "6" Then
                ' Inserir na coluna Consideraﾃｧﾃｵes PIS/COFINS o valor "Produto Sujeito ﾃ Alﾃｭquota Zero"
                wsCliente.Cells(i, colConsideracoespis).Value = "Produto Sujeito ﾃ Alﾃｭquota Zero"
                
            End If
        End If
        
         Next i
    Next cel
    
    ' Reativar atualizaﾃｧﾃｵes da tela
    Application.ScreenUpdating = True

End Sub

' Funﾃｧﾃ｣o auxiliar para obter o ﾃｭndice da coluna por nome
Function ObterIndiceColunaPorNome(ws As Worksheet, nomeColuna As String) As Long
    Dim cel As Range
    For Each cel In ws.Rows(1).Cells
        If cel.Value = nomeColuna Then
            ObterIndiceColunaPorNome = cel.Column
            Exit Function
        End If
    Next cel
    ObterIndiceColunaPorNome = -1
End Function
```

<p>
  Salve as macros criadas e feche o VBA
</p>

<img src="Imagens\salvandoMacros.png">
<img src="Imagens\fechandoVBA.png">

<p>
  Com as planilhas abertas apertamos "ALT + F8" para exibir as macros disponﾃｭveis, definimos um atalho para cada macro, no nosso exemplo iremos usar "CRTL + q" para preencher a coluna "Consideraﾃｧﾃｵes ICMS" e "CRTL + m" para preencher a coluna "Consideraﾃｧﾃｵes PIS/COFINS"
</p>

<img src="Imagens\definindoAtalhoMacro.png">
<img src="Imagens\definindoAtalhoPiseCofins.png">

<p>
Agora todas as vezes que selecionamos uma linha na planilha "Audit.xlsm" e apertamos "CRTL + q" a macro ﾃｩ executada, oque a macro em VBA faz ﾃｩ procurar o produto que foi selecionado na planilha "Audit.xlsm" dentro da planilha "Cliente.xlsm", ao encontrar esse produto compara os dados do mesmo produto em ambas as planilhas e traz um comentﾃ｡rio referente a comparaﾃｧﾃ｣o na coluna "Consideraﾃｧﾃｵes ICMS" dentro da planilha "Cliente.xlsm". Lembrando que todos os produtos que estejam dentro da seleﾃｧﾃ｣o serﾃ｣o auditados, entﾃ｣o quanto mais produtos selecionar, maior serﾃ｡ o consumo de memﾃｳria RAM no momento que a macro for executada.
</p>

<img src="Imagens\macroFuncionandoICMS.gif">

<p>
A mesma lﾃｳgica serve para auditar o PIS e COFINS do produto, a diferenﾃｧa ﾃｩ que iremos usar o atalho "CRTL + m" e o comentﾃ｡rio serﾃ｡ inserido na coluna "Consideraﾃｧﾃｵes PIS/COFINS".
</p>

<img src="Imagens\pisEcofinsAutom.gif">

<p>
A macro em VBA  que preenche a coluna "Consideraﾃｧﾃｵes ICMS" segue uma lﾃｳgica para poder inserir o comentﾃ｡rio correto, abaixo mostramos a lista dos critﾃｩrios que devem existir para que os comentﾃ｡rios sejam inseridos:
</p>

- Cliente usando CST 00 ou 20 e auditor usando CST 40

Produto se enquadra em isenﾃｧﾃ｣o

- Cliente usando CST 00 ou 20 e auditor usando CST 60

Produto se enquadra em ST

- Cliente usando CST 20 e auditor nﾃ｣o usa nenhum CST

Produto nﾃ｣o se enquadra em reduﾃｧﾃ｣o

- Cliente usando CST 40 e auditor usando CST 00, 20 ou 40

Produto nﾃ｣o se enquadra em isenﾃｧﾃ｣o

- Cliente usando CST 60 e auditor usando CST 00, 20 ou 40

Produto nﾃ｣o se enquadra em ST

- Cliente usando CST 00 e auditor usando CST 20 com CBENEF de cesta bﾃ｡sica

Produto se enquadra em reduﾃｧﾃ｣o para 7%

- Cliente usando CST 00 e auditor usando CST 20 com CBENEF de reduﾃｧﾃ｣o de 19% para 12%

Produto se enquadra em reduﾃｧﾃ｣o de 19% para 12%

- Cliente usando CST 00 e auditor usando CST 00 porﾃｩm a alﾃｭquota do cliente estﾃ｡ diferente da alﾃｭquota do auditor

Alﾃｭquota Incorreta

- Cliente usando alﾃｭquota diferente de 21 sem o adicional de protege e auditor usando alﾃｭquota 21

Alﾃｭquota Incorreta(ICMS 19% + 2% FCP)

- Cliente usando alﾃｭquota diferente de 27 sem o adicional de protege e auditor usando alﾃｭquota 27

Alﾃｭquota Incorreta(ICMS 25% + 2% FCP)

- Cliente usando CST 41 e auditor usando CBENEF que nﾃ｣o se enquadra no CST 41

Produto Tributado

- Cliente usando CST 00, 20 ou 40 e auditor usando CBENEF que se enquadra no CST 41

Produto nﾃ｣o Tributado

- Quando os dados de CST e alﾃｭquota do cliente sﾃ｣o iguais aos dados do auditor

Ok Conferido

<p>
A macro em VBA  que preenche a coluna "Consideraﾃｧﾃｵes PIS/COFINS" segue uma lﾃｳgica para poder inserir o comentﾃ｡rio correto, abaixo mostramos a lista dos critﾃｩrios que devem existir para que os comentﾃ｡rios sejam inseridos:
</p>

- Quando o auditor usa 1 no CST e o cliente usa algum CST diferente de 1

Produto Tributado

- Quando o auditor usa 4 no CST e o cliente usa algum CST diferente de 4

Produto Monofﾃ｡sico

- Quando o auditor usa 5 no CST e o cliente usa algum CST diferente de 5

Substituiﾃｧﾃ｣o Tributﾃ｡ria

- Quando o auditor usa 6 no CST e o cliente usa algum CST diferente de 6

Produto Sujeito ﾃ Alﾃｭquota 0

- Quando o cliente usa um CST igual ao do auditor

Ok Conferido

## 3 CONCLUSﾃグ

<p>
No final do processo basta retornar a planilha "Cliente.xlsm" auditada para o cliente.
</p>

## 4 FERRAMENTAS UTILIZADAS

- Excel

- Visual Basic for Applications

- Visual Studio Code

- Notepad++

- Git Hub






